#!/usr/bin/perl

use Verse;
use Verse::Demo;
use Getopt::Long;
my %OPTIONS = ();
GetOptions(\%OPTIONS, qw/
	local|l
/);

$ENV{VERSE_LOCAL} = 1 if $OPTIONS{local};

sub file
{
	my ($path, $contents) = @_;
	!-f $path or die "$path already exists.\n";
	open my $fh, ">", $path
		or die "Failed to create $path: $!\n";
	print $fh $contents;
	close $fh;
}

my $CMD = @ARGV ? $ARGV[0] : 'help';
if ($CMD eq 'build') {
	rhyme;

	my $theme = verse->{paths}{theme}.'/render';
	die "Failed to find theme file: $theme\n"
		unless -x $theme;

	eval { require $theme } or die "$@\n";
	exit 0;
}

if ($CMD eq 'run' or $CMD eq 'demo') {
	demo htdocs => 'htdocs';
}

if ($CMD eq 'new' or $CMD eq 'setup') {
	my $dir = shift @ARGV;
	if ($dir) {
		!-d $dir or die "$dir/ already exists!\n";
		mkdir $dir;
		chdir $dir;
	}
	!-f "site.yml" or die "site.yml file already exists!\n";
	!-f "Makefile" or die "Cowardly refusing to clobber your existing Makefile\n";

	mkdir $_ for (qw(data theme));

	file "Makefile", <<EOF;
# Makefile generated by $0 $@

build:
	rm -rf htdocs
	verse build

local:
	rm -rf htdocs
	verse build --local

check:
	linkchecker htdocs/index.html  --no-follow-url=http:/

EOF

	file "site.yml", <<EOF;
#
#
#           Welcome to Verse!
#
#
# This is your site.yml file.
# All of your metadata and content is kept alongside this file, and
# verse parses, assembles and formats the final site in htdocs/.
#
# To get started, you will probably want to change the title of
# your site, and maybe select a different theme!
#

site:
  title: My New Verse Site
  theme: default

#
# P.S. If you want to get started adding content to your site,
# you'll be interested in the files under data/:
#
#  - data/pages - Standalone pages (index, About, etc.)
#  - data/blog  - Articles in a weblog
#
#
# Happy Hacking!
# - The Verse Team
EOF

	exec "/usr/bin/env editor site.yml";
	exit 0;
}

print "USAGE: $0 (new|build|run|help)\n";
exit 1;

=head1 NAME

verse - Static Website Generator

=head1 DESCRIPTION

Tired of installing and configuring web application servers and databases?

Sick of the load that dynamic CMS and blog software put on your cloud VM?

Still want layout and templating that these tools provide?

Love text files?

Verse is the website software for you!  Harkening back to the early days
of the web, Verse revives the near-dead art of hand-crafted HTML and
static (read:fast) web sites.

=head1 AUTHOR

James Hunt C<< <james@niftylogic.com> >>

=cut
